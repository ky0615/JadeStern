apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

apply from: '../signingConfigs.gradle', to: android

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "ws.temp.jadestern"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "TWITTER_CONSUMER_KEY", "${project.property("twitter.consumerkey")}"
        buildConfigField "String", "TWITTER_CONSUMER_SECRET", "${project.property("twitter.consumersecret")}"

        manifestPlaceholders = [
                fabric_api_key: project.property("fabric.api_key")
        ]

    }
    buildTypes {

        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        local {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        optimize {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:support-v4:$support_library"
    compile "com.android.support:appcompat-v7:$support_library"
    compile "com.android.support:design:$support_library"
    compile "com.android.support:cardview-v7:$support_library"
    compile "com.android.support:customtabs:$support_library"

    compile 'com.google.android.gms:play-services-analytics:8.1.0'

    apt 'org.projectlombok:lombok:1.16.6'
    provided 'org.projectlombok:lombok:1.16.6'
    compile 'org.twitter4j:twitter4j-core:4.0.4'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'io.realm:realm-android:0.87.5'
    compile 'io.reactivex:rxandroid:1.1.0'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}
